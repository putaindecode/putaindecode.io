---
date: "2017-11-09"
title: "ReasonReact, pour une UI qu'elle est bien typ√©e"
tags:
  - reasonml
  - ocaml
  - react
authors:
  - bloodyowl
header:
  image: index.png
  linearGradient: #DD4B39, #DD4B39
---

Si comme moi, depuis l‚Äôapparition de React, vous vous √™tes de plus en int√©ress√©s au typage pour vos applications front (c'est √ßa de commencer avec JS‚Ä¶), vous avez certainement utilis√© les `propTypes` au d√©but en vous disant "putain c'est cool de v√©rifier les types, √ßa m'√©vite bien des probl√®mes". Puis c‚Äô√©tait sympa mais bon, faut quand m√™me ex√©cuter le bout de code qui p√®te et il est peut-√™tre super chiant d'y acc√©der dans l'app. Du coup, vous vous √™tes s√ªrement tourn√©s vers Flow ou TypeScript.

Dans cet article, on va d√©couvrir la *next-step* dans ce cheminement : √©crire nos composants React dans un langage statiquement et fortement typ√©: Reason üöÄ. Reason, c'est OCaml, avec son type-system puissant et une syntaxe plus simple quand on vient du JS. Si vous n‚Äôavez pas lu [l‚Äôintroduction √† ce langage](/fr/articles/reason/introduction-reason/), c‚Äôest le moment.

L√†, je vais vous pr√©senter **ReasonReact**, des bindings API par dessus React support√©s officiellement par l'√©quipe de Reason. Facebook *dogfood* la solution puisqu'elle est utilis√©e sur messenger.com pour la majeure partie de ses composants.

## Stateless

Commen√ßons par le traditionnel HelloWorld‚Ñ¢ :

```js
/* Un composant ReasonReact se cr√©e en deux temps: d'abord on cr√©e le `component`
   √† partir d‚Äôun appel √† `statelessComponent` ou `reducerComponent` (il existe d'autres
   cas plus avanc√©s, mais on s'y attardera pas dans cet article d'introduction). */
let component = ReasonReact.statelessComponent("HelloWorld");

/* Ensuite, on d√©clare une fonction `make` qui prend des arguments nomm√©s
   (qui √©quivalent aux `props` de React) et un dernier argument non-nomm√©,
   contenant les `children`. Cette fonction doit retourner un record, dans
   lequel on spread notre `component` et dans lequel on d√©finit une propri√©t√©
   `render` qui prend comme param√®tre `self` (√©quivalent du `this`) et qui retourne
   un √©l√©ment React. L√†-dessus √ßa devrait pas trop vous chambouler de ce que
   vous connaissez de React.
   On peut remarquer que les props sont les arguments de la fonction `make`,
   comme avec les composants fonctionnels de React.*/
let make = (~message, _children) => {
  ...component,
  render: (_self) =>
    <div>
      (ReasonReact.stringToElement message)
    </div>
};
```

Et pour monter le composant :

```js
ReactDOMRe.renderToElementWithId(<HelloWorld message="Helloworld" />, "root");
```

Un des gros avantages √† utiliser Reason, c‚Äôest que le langage est capable d‚Äôinf√©rer la grande majorit√© des types et sera en mesure de d√©tecter dans toute l‚Äôapp si quelque chose n‚Äôest pas pass√© correctement : pour le langage, il s‚Äòagit simplement de fonctions qui appellent d‚Äôautres fonctions, et les langages fonctionnels statiquement et fortement typ√©s sont plut√¥t pas d√©gueulasses pour √ßa.

## Stateful

La petite particularit√© de ReasonReact vis √† vis des composants stateful, c‚Äôest que les mises √† jour d'√©tats doivent passer par un reducer, comme si chaque composant embarquait sa petite impl√©mentation de redux.

Maintenant, comment qu'on fait pour cr√©er un composant stateful ?

On commence par d√©finir le type du state : contrairement √† JS, il ne s'agit pas forc√©ment d'un objet, √ßa peut √™tre une cha√Æne de caract√®re, un entier, un variant, un boolean, un arbuste, une map, un jus de fruits frais, un tableau, whatever.

```js
type state = {
  counter: int
};
```

On va d√©finir notre type action, sous la forme de variants: chaque variant repr√©sente un des type d‚Äôaction possible. Pour bien se repr√©senter ce qu'est une action, c‚Äôest un token, contenant possiblement des param√®tres, qu‚Äôon va envoyer √† notre fameux reducer qui, lui, retournera une r√©action √† cette action.

```js
type action =
  | Increment
  | Decrement;
```

Dans le composant retourn√© par `make`, on ajoute une fonction `initialState` qui retourne‚Ä¶ l'√©tat initial (c'est bien, vous suivez), et une fonction `reducer`, qui effectue un pattern-matching sur l‚Äôaction et retourne une update.
Cette fonction prend deux param√®tres: l'`action` √† traiter et le `state` √† jour (comme lorsque l'on passe un callback √† `setState` dans l'√©quivalent JavaScript `setState(state => newState)`).

L‚Äôupdate retourn√©e indique au component comment il doit se mettre √† jour (ici sont list√©s les cas courants) :

- `NoUpdate`, pour ne rien faire
- `Update`, pour mettre √† jour l‚Äô√©tat et re-rendre le composant
- `SideEffect` pour lancer un effet de bord (e.g. une requ√™te r√©seau)
- `UpdateWithSideEffect`, pour changer le state et lancer un effet de bord (e.g. afficher un loader et lancer une requ√™te)

*Wrapping up* :

```js
type state = {counter: int};

type action =
  | Increment
  | Decrement;

/* Il faut bien d√©finir le `component` **apr√®s** les types `state` et `action`, pour qu'il puisse les lire */
let component = ReasonReact.reducerComponent("Count");

let make = (~initialCounter=0, _) => {
  ...component,
  initialState: () => {counter: initialCounter},
  reducer: (action, state) =>
    /* Toutes mes updates passent par l√†, bien pratique pour qu'une
      personne puisse aborder rapidement le composant */
    switch action {
    | Increment => ReasonReact.Update({counter: state.counter + 1})
    | Decrement => ReasonReact.Update({counter: max(0, state.counter - 1)})
    },
  render: ({state, reduce}) =>
    <div>
      (ReasonReact.stringToElement(state.counter |> string_of_int))
      /* La fonction reduce prend une fonction qui retourne l'action.
           Il s'agit d'une fonction pour lire les propri√©t√©s des
           events (qui sont pooled dans React) de mani√®re synchrone, alors
           que le reducer est appel√© de mani√®re asynchrone.
         */
      <button onClick=(reduce((_event) => Decrement))> (ReasonReact.stringToElement("-")) </button>
      <button onClick=(reduce((_event) => Increment))> (ReasonReact.stringToElement("+")) </button>
    </div>
};
```

et hop:

```js
ReactDOM.renderToElementWithId(<Count initialCount=0 />, "App");
```

## With side-effects

Bien que √ßa puisse para√Ætre un peu lourd de devoir faire un `reducer` pour g√©rer ses updates, √ßa apporte quand m√™me:

- Un seul endroit par composant o√π toutes les updates passent
- La possibilit√© pour le compiler de d√©tecter si l'on oublie de g√©rer des actions
- De g√©rer lisiblement et uniform√©ment les effets de bord

<img src="./terminal.png" alt="" />

Exemple ici avec un composant o√π on va faire comme si on r√©cup√©rait l'utilisateur connect√© sur une API.

```js
let resolveAfter = (ms) =>
  Js.Promise.make(
    (~resolve, ~reject as _) => ignore(Js.Global.setTimeout(() => [@bs] resolve(ms), ms))
  );

module User = {
  type t = {username: string};
  /* faisons comme si on avait un appel serveur
     (je le fais comme √ßa pour que vous puissiez copier/coller le code
     pour essayer chez vous) */
  let getUser = (_) =>
    resolveAfter(1000)
    |> Js.Promise.then_(
         (_) =>
           Js.Promise.resolve({
             username: "MyUsername" ++ string_of_int(Js.Math.random_int(0, 9999))
           })
       );
};

/* Le "user" distant peut avoir 4 √©tats possibles ici */
type resource('a) =
  | Inactive
  | Loading
  | Idle('a)
  | Errored;

type action =
  | Load
  | Receive(resource(User.t));

type state = {user: resource(User.t)};

let component = ReasonReact.reducerComponent("User");

let getUser = (credentials, {ReasonReact.reduce}) =>
  ignore(
    User.getUser(credentials)
    /* Si tout s'est bien pass√© */
    |> Js.Promise.then_(
         /* On peut utiliser les actions en dehors du `make`: c'est juste des variants */
         (payload) => Js.Promise.resolve(reduce((payload) => Receive(Idle(payload)), payload))
       )
    /* Si √ßa a merd√© */
    |> Js.Promise.catch((_) => Js.Promise.resolve(reduce(() => Receive(Errored), ())))
  );

let make = (~credentials, _) => {
  ...component,
  initialState: () => {user: Inactive},
  reducer: (action, _state) =>
    switch action {
    /* UpdateWithSideEffects met √† jour l'√©tat, puis lance l'effet de bord,
       tr√®s pratique pour ce genre de cas */
    | Load => ReasonReact.UpdateWithSideEffects({user: Loading}, getUser(credentials))
    | Receive(user) => ReasonReact.Update({user: user})
    },
  didMount: ({reduce}) => {
    reduce(() => Load, ());
    ReasonReact.NoUpdate
  },
  render: ({state, reduce}) =>
    <div>
      (
        ReasonReact.stringToElement(
          switch state.user {
          | Inactive
          | Loading => "Loading ..."
          | Idle(user) => "Hello " ++ user.username
          | Errored => "An error occured"
          }
        )
      )
      <div>
        <button
          disabled=(
            switch state.user {
            | Idle(_) => Js.false_
            | _ => Js.true_
            }
          )
          onClick=(reduce((_) => Load))>
          (ReasonReact.stringToElement("Reload"))
        </button>
      </div>
    </div>
};
```

## Oui mais est-ce que je peux l'utiliser aujourd'hui alors que j'ai d√©j√† une grosse codebase React ?

<img src="./tenor.gif" alt="" />

Pour utiliser des composants ReasonReact avec React

```js
let jsComponent =
  ReasonReact.wrapReasonForJs(
    ~component,
    (jsProps) =>
      make(
        ~credentials=jsProps##credentials,
        [||]
      )
  );
```

et

```js
const MyComponent = require("path/to/reason/output").jsComponent;
```

√Ä l'inverse, pour utiliser des composants React avec ReasonReact

```js
[@bs.module "path/to/good/old/reactjs/component"] external myJsComponent : ReasonReact.reactClass = "default";

let make = (~message: string, _children) =>
  ReasonReact.wrapJsForReason(
    ~reactClass=myJsComponent,
    ~props={"message": message},
    [||]
 ¬†);
```

Voil√† pour les *basics* de ReasonReact. Pour en savoir plus, y a [la petite doc qui va bien](https://reasonml.github.io/reason-react/), et on vous pr√©parera un petit article sur les aspects un peu plus avanc√©s de l'usage.

Bisous bisous.
